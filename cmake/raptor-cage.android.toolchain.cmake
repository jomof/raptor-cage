
set(RAPTOR_CAGE_ENABLED true)

function(get_raptor_cage_key_file return_property_name)
  set(USER_HOME $ENV{HOME})
  if(NOT EXISTS ${USER_HOME})
     message(FATAL_ERROR "Could not locate home directory")
  endif()
  set(RAPTOR_CAGE_HOME ${USER_HOME}/.raptor-cage/ndk-cache)
  if(NOT EXISTS ${RAPTOR_CAGE_HOME})
    file(MAKE_DIRECTORY ${RAPTOR_CAGE_HOME})
  endif()

  # Read the NDK source.properties file
  file(READ "${ANDROID_NDK}/source.properties" RAPTOR_CAGE_NDKSOURCE_PROPERTIES)

  set(RAPTOR_CAGE_NDKREVISION_REGEX
    "^Pkg\\.Desc = Android NDK\nPkg\\.Revision = ([0-9]+)\\.([0-9]+)\\.([0-9]+)(-beta([0-9]+))?")
  if(NOT RAPTOR_CAGE_NDKSOURCE_PROPERTIES MATCHES "${RAPTOR_CAGE_NDKREVISION_REGEX}")
    message(SEND_ERROR "Failed to parse Android NDK revision: ${ANDROID_NDK}/source.properties.\n${RAPTOR_CAGE_NDKSOURCE_PROPERTIES}")
  endif()

  set(RAPTOR_CAGE_NDKMAJOR "${CMAKE_MATCH_1}")
  set(RAPTOR_CAGE_NDKMINOR "${CMAKE_MATCH_2}")
  set(RAPTOR_CAGE_NDKBUILD "${CMAKE_MATCH_3}")
  set(RAPTOR_CAGE_NDKBETA "${CMAKE_MATCH_5}")
  if(RAPTOR_CAGE_NDKBETA STREQUAL "")
    set(RAPTOR_CAGE_NDKBETA "0")
  endif()
  set(RAPTOR_CAGE_NDKREVISION "${RAPTOR_CAGE_NDKMAJOR}.${RAPTOR_CAGE_NDKMINOR}.${RAPTOR_CAGE_NDKBUILD}${CMAKE_MATCH_4}")
  string(SHA1 RAPTOR_CAGE_ANDROID_NDK_HASH ${ANDROID_NDK})

  set(
    ${return_property_name}
    "${RAPTOR_CAGE_HOME}/${ANDROID_TOOLCHAIN}_${RAPTOR_CAGE_NDKREVISION}_${ANDROID_ABI}_${ANDROID_PLATFORM}_${CMAKE_VERSION}_${RAPTOR_CAGE_ANDROID_NDK_HASH}.cmake"
    PARENT_SCOPE)

endfunction()

function(raptor_cage)
  get_raptor_cage_key_file(RAPTOR_CAGE_PRESETS_FILE)
  file(WRITE ${RAPTOR_CAGE_PRESETS_FILE} "# Generated file, do not edit\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_COMPILER_FORCED true)\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_COMPILER_FORCED true)\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C11_COMPILE_FEATURES ${CMAKE_C11_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C90_COMPILE_FEATURES ${CMAKE_C90_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C99_COMPILE_FEATURES ${CMAKE_C99_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX11_COMPILE_FEATURES ${CMAKE_CXX11_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX17_COMPILE_FEATURES ${CMAKE_CXX17_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX98_COMPILE_FEATURES ${CMAKE_CXX98_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_ABI_COMPILED ${CMAKE_CXX_ABI_COMPILED})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_COMPILER_ABI ${CMAKE_CXX_COMPILER_ABI})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_ABI_COMPILED ${CMAKE_C_ABI_COMPILED})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_COMPILER_ABI ${CMAKE_C_COMPILER_ABI})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_COMPILE_FEATURES ${CMAKE_C_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_INTERNAL_PLATFORM_ABI ${CMAKE_INTERNAL_PLATFORM_ABI})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CXX_TEST_WAS_RUN ${CXX_TEST_WAS_RUN})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(C_TEST_WAS_RUN ${C_TEST_WAS_RUN})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_SIZEOF_DATA_PTR ${CMAKE_CXX_SIZEOF_DATA_PTR})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_SIZEOF_DATA_PTR ${CMAKE_C_SIZEOF_DATA_PTR})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_SIZEOF_VOID_P ${CMAKE_SIZEOF_VOID_P})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX14_COMPILE_FEATURES ${CMAKE_CXX14_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_COMPILE_FEATURES ${CMAKE_CXX_COMPILE_FEATURES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_IMPLICIT_LINK_LIBRARIES ${CMAKE_C_IMPLICIT_LINK_LIBRARIES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_IMPLICIT_LINK_LIBRARIES ${CMAKE_CXX_IMPLICIT_LINK_LIBRARIES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_IMPLICIT_LINK_LIBRARIES ${CMAKE_C_IMPLICIT_LINK_LIBRARIES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_CXX_IMPLICIT_LINK_DIRECTORIES ${CMAKE_CXX_IMPLICIT_LINK_DIRECTORIES})\n")
  file(APPEND ${RAPTOR_CAGE_PRESETS_FILE} "set(CMAKE_C_IMPLICIT_LINK_DIRECTORIES ${CMAKE_C_IMPLICIT_LINK_DIRECTORIES})\n")
endfunction()

if (DEFINED ANDROID_NDK)
  get_raptor_cage_key_file(RAPTOR_CAGE_PRESETS_FILE)
  include(${ANDROID_NDK}/build/cmake/android.toolchain.cmake)
  set(CMAKE_SYSTEM_VERSION 1)
  if (EXISTS ${RAPTOR_CAGE_PRESETS_FILE})
    message(STATUS "Raptor Cage intercepted compiler detection and used cached results instead.")
    include(${RAPTOR_CAGE_PRESETS_FILE})
  endif()
endif()

